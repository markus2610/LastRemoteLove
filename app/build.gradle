apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'robolectric'
apply plugin: 'crashlytics'

android {
    compileSdkVersion TARGET_SDK_VERSION
    buildToolsVersion BUILD_TOOLS_VERSION

    defaultConfig {
        applicationId "com.meoyawn.remotelove"

        minSdkVersion MIN_SDK_VERSION
        targetSdkVersion TARGET_SDK_VERSION

        versionCode 3
        versionName "0.1"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    kotlinOptions.annotations = file("$rootDir/annotations/")
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }

    final isTravis = "true".equals(System.getenv("TRAVIS"))
    if (!isTravis) {
        signingConfigs {
            release {
                storeFile file(KEYSTORE)
                storePassword PASSWORD
                keyAlias ALIAS
                keyPassword PASSWORD
            }
        }
        buildTypes {
            release {
                signingConfig signingConfigs.release
                runProguard false
                proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro', "$rootDir/dagger-proguard-keepnames.cfg"
            }
        }
    }
    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }
}

ext {
    mementoVersion = '0.2'
    daggerVersion = '1.2.2'
    esperandroVersion = '2.1.0'
    rxJavaVersion = '0.20.6'
}

repositories {
    maven { url 'http://nexus.idamob.ru/content/groups/public' } // dagger proguard helper
    maven { url 'http://download.crashlytics.com/maven' }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"

    compile 'com.squareup.okhttp:okhttp:2.0.0' // HTTP client
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.0.0'
    compile 'com.squareup.picasso:picasso:2.3.4' // image loading
    compile 'com.squareup.retrofit:converter-jackson:1.7.1'

    provided "de.devland.esperandro:esperandro:$esperandroVersion" // shared prefs boilerplate
    compile "de.devland.esperandro:esperandro-api:$esperandroVersion"
    compile "de.devland.esperandro:esperandro-jackson-addon:$esperandroVersion"

    compile "com.squareup.dagger:dagger:$daggerVersion" // DI
    provided "com.squareup.dagger:dagger-compiler:$daggerVersion"
    compile "com.netflix.rxjava:rxjava-android:$rxJavaVersion" // asynchrony and events
    compile "com.netflix.rxjava:rxjava-kotlin:$rxJavaVersion"
    provided 'com.github.idamobile:dagger-proguard-helper-processor:1.0-SNAPSHOT'

    compile 'com.jakewharton:butterknife:5.1.2' // view injection

    compile 'com.jakewharton.timber:timber:2.4.1' // logging
    compile 'commons-codec:commons-codec:1.9'
    compile 'com.github.dmytrodanylyk.circular-progress-button:library:1.1.3'
    provided 'org.projectlombok:lombok:1.14.8'
    compile 'com.melnykov:floatingactionbutton:1.0.5'
    compile 'com.readystatesoftware.systembartint:systembartint:1.0.4'
    compile 'com.crashlytics.android:crashlytics:1.1.13'

    androidTestCompile 'org.robolectric:robolectric:2.3'
    androidTestCompile 'junit:junit:4.12-beta-2'
    androidTestCompile 'com.squareup.assertj:assertj-android:1.0.0'
}

configurations {
    compile.exclude module: 'gson' // we prefer jackson over gson for more convinient configuration
    compile.exclude module: 'guava' // some deps drag this monster into the final APK
    androidTestCompile.exclude module: 'commons-logging'
    androidTestCompile.exclude module: 'httpclient'
}